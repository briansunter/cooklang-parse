/**
 * Cooklang Grammar for Ohm
 * Based on the official Cooklang specification at https://cooklang.org/docs/spec/
 */

Cooklang {
  // Main recipe structure
  Recipe
    = (MetadataDirective | blankLine)* Metadata? RecipeItem*

  // Recipe items can be interleaved
  RecipeItem
    = Section | MetadataDirective | Note | CommentLine | blankLine | spaceOnly | Step

  // YAML front matter metadata
  Metadata
    = "---" YamlContent "---"

  YamlContent
    = (~"---" any)*

  // Named section ==Name==
  Section
    = "==" sectionName "=="      -- double
    | "=" sectionName            -- single

  sectionName
    = (~"==" ~newline any)+

  // Recipe step - paragraphs separated by blank lines
  Step
    = StepLine+

  StepLine
    = ~(hspace* "-- ") ~(hspace* newline) ~sectionStart ~MetadataDirective StepItem+ InlineComment? "\n"?

  sectionStart
    = "==" | "=" ~"=" ~"@"

  // Items that can appear in a step
  StepItem
    = Ingredient | Cookware | Timer | Text

  // Text without special Cooklang syntax
  Text
    = (~("\n" | "\r\n" | "\r" | "@" &ingredientStartChar | "#" &cookwareStartChar | "~" &wordChar | "~" &"{" | "-- ") any)+

  ingredientStartChar
    = wordChar | "@" | "&" | "?" | "+" | "-"

  cookwareStartChar
    = wordChar | "&" | "?" | "+" | "-"

  // Ingredient: @name{quantity%unit} or @multi word name{}
  Ingredient
    = "@" ingredientModifiers ingredientWord (hspace+ ingredientWord)+ ingredientAmount ingredientNote?  -- multi
    | "@" ingredientModifiers ingredientWord ingredientAmount? ingredientNote?                           -- single

  ingredientNote
    = "(" (~")" ~newline any)* ")"

  ingredientModifiers
    = ("@" | "&" | "?" | "+" | "-")*

  ingredientWord
    = componentWordChar+

  ingredientAmount
    = "{" (~"}" any)* "}"

  // Cookware: #name or #multi word{}
  Cookware
    = "#" cookwareModifiers cookwareWord (hspace+ cookwareWord)+ cookwareAmount cookwareNote?   -- multi
    | "#" cookwareModifiers cookwareWord cookwareAmount? cookwareNote?                         -- single

  cookwareNote
    = "(" (~")" ~newline any)* ")"

  cookwareModifiers
    = ("&" | "?" | "+" | "-")*

  cookwareWord
    = componentWordChar+

  cookwareAmount
    = "{" (~"}" any)* "}"

  // Timer: ~{quantity%unit} or ~name{quantity%unit}
  Timer
    = "~" timerName? "{" timerQuantity timerUnit? "}"   -- withAmount
    | "~" timerName                                     -- word

  timerName
    = word+

  timerQuantity
    = (~"%" ~"}" any)+

  timerUnit
    = "%" (~"}" any)+

  // Note lines (start with >)
  Note
    = ">" noteText Newline?

  noteText
    = (~"\n" ~"\r" ~">" any)+

  // Metadata directive: >> key: value
  MetadataDirective
    = hspace* ">>" directiveBody Newline?

  directiveBody
    = hspace* directiveKey hspace* ":" directiveValue

  directiveKey
    = (~":" ~newline any)+

  directiveValue
    = (~newline any)*

  // Inline comment: -- comment
  InlineComment
    = "-- " (~Newline any)*

  // Full-line comments
  CommentLine
    = hspace* InlineComment Newline?

  // Component name word char - wordChar plus | for alias syntax
  componentWordChar
    = wordChar | "|"

  // Basic tokens
  word
    = wordChar+

  wordChar
    = letter | digit | "_" | "-" | emoji | otherWordChar

  emoji
    = "\u{1F600}".."\u{1F64F}"
    | "\u{1F300}".."\u{1F5FF}"
    | "\u{1F680}".."\u{1F6FF}"
    | "\u{2600}".."\u{26FF}"
    | "\u{2700}".."\u{27BF}"
    | "\u{1F900}".."\u{1F9FF}"
    | "\u{1FA70}".."\u{1FAFF}"

  otherWordChar
    = "\u00C0".."\u00FF"
    | "\u0100".."\u017F"
    | "\u0400".."\u04FF"

  newline
    = "\n" | "\r\n" | "\r"

  Newline
    = newline

  blankLine
    = hspace* newline

  spaceOnly
    = hspace+ &end

  hspace
    = " " | "\t"

  // Keep Ohm's implicit skipping from crossing lines.
  space := "\t"
}
